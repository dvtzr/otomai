## Aoi Route
label aoi_route:
    
    # first event: broken cosplay
    label aoi_route_1:

        ## show aoi
        show aoi happy at center
        with dissolve

        ao "סוף סוף נכנסנו!"

        ao "מהר, מהר! אנחנו צריכים להתחיל לעבוד!"

        ao "אין לנו זמן לבזבז על \"שלום שלום\" או \"אפשר תמונה?\", יש לי תחרות לנצח!"

        ao "דבר ראשון והכי חשוב, אנחנו צריכים למצוא לנו– "
        
        with vpunch 
        show aoi surprised
        
        "אחד מבאי הכנס לא הסתכל ונתקל בטעות באאוי"

        stop music
        play music bgm_crisis
        "אוי! הכוכב שישב לאאוי במרכז המטה התנתק מהמקום ונפל!"

        st "אוי, אני מצטער!"

        st "בדיוק עבר פה מישהו מחופש לאקאנוסקה, וכל כך התרגשתי שלא שמתי לב לאן אני הולך!"

        p "אוי לא? את בסדר?"

        ao angry "אני בסדר, אבל איזו חוצפה! למה מתלהבים כל כך ממר אקאנוסקה ולא ממני!"

        show aoi angry at left_to_right
        "אאוי מסתובבת כדי לראות אם באמת היה קוספלייר כזה או אחר של אקאנוסקה באיזור, כדי לנזוף בו על זה שהמעריצים שלו לא זהירים. במקום זה, היא רואה את אקאי האמיתי, שנראה מוקף מכל עבר במעריצים."

        hide aoi angry with dissolve
        show akai happy with dissolve
        ak "תודה, תודה, אני באמת מאוד גבוה."
        
        ak stress "אבל אני חייב ללכת! אני חייב להספיק להגיע לתדריך מתנדבים!"

        hide akai with dissolve 
        "בחוסר ברירה, אקאי מסתכל לכל הכיוונים בשביל למצוא משהו שיסיח את דעת המעריצים שלו."

        show aoi angry with dissolve
        "הוא רואה את אאוי, ומפספס את המבט הרצחני שלה."

        hide aoi angry with dissolve
        show akai stress with dissolve

        ak "תסתכלו שם!"
        
        ak "הנה אאוי בקוספליי ממש חמוד!"
        
        ak "כמו בפרסומים בסושיאל!"

        hide akai stress
        with dissolve

        "וברגע שהמעריצים מסתובבים… אקאי מתחיל לרוץ במהירות לכיוון המדרגות. אאוי אפילו לא מספיקה לצעוק עליו."

        show aoi angry with hpunch

        ao "למה הוא תמיד עושה לי את זה!"
        
        ao "אני נשבעת, כשאני אתפוס אותו, התדריך יהיה הדבר האחרון שהוא יצטרך לדאוג ממנו!"
        
        ao surprised "…רגע אחד. הכוכב הנוצץ שמלכת השיפקה הקריבה את חייה בשבילו! הוא נפל!"
        
        ao "לקח לי שבועיים למדל אותו בתלת!!!"

        ao sad "אוף, עכשיו בחיים לא יזהו אותי בתור הנסיכה נטלישיקו!"

        p "מי?"

        ao angry "הנסיכה נטלישיקו? מחלק 8 של הסדרה \"הפעם ההיא שהפכתי לסליים, אבל בצפון תל אביב\"? "

        menu:
            "מי?" :
                pass
            "מה?" :
                pass
            "מו?" :
                pass

        ao smug "מה אתה לא רואה אנימה? זו הסדרה הכי פופולרית כרגע, היא אפילו זכתה בטקס פרסי האנימה האחרון!"

        ao "ועוד במיוחד עשיתי את הגרסה שלה אחרי שינוי הצורה של הירח, אבל לפני הסאגה של ברקיטו!"

        menu:
            "אה, בטח, איך לא זיהיתי ישר?" :
                pass
            "א-את בטוחה שאת לא ממציאה?" :
                pass
            "חשבתי שאת אחת מהאלו, נו, טיילור טנשי?" :
                pass
        
        ao worry "אוי לא, אם לא זיהית את הגרסה הזו ככה, איך יזהו בלי הכוכב הנוצץ? היא מקבלת אותו רק בחלק ממש מאוחר של הסדרה!"

        ao "אני לא יכולה ללכת לשיפוט קוספליי ככה!"

        ao "אם גם הם לא יזהו אותי זה יהיה ממש ממש מביך!"

        p "הממממ אולי יש מקום שאפשר לתקן?"

        menu:
            "אולי יש מקום שמתקנים בו קוספליי?":
                jump aoi_route_1_good
            "אולי לקפיטריה יש דבק?":
                jump aoi_route_1_bad
        
        # good choice
        label aoi_route_1_good:
            
            ao surprised "עמדת תיקון הקוספליי! כמובן, איך לא חשבתי על זה?"
            
            p "עמדת תיקון הקוספליי?"
            
            p "ראיתי משהו כזה במפה, אבל הם באמת יכולים לעזור?"

            ao smug "ברור שכן!"

            ao "יש שם את כל הכלים והאנשים לעזור לקוספליירים במקרי חירום!"

            p "אם ככה, אני בטוח שאם נלך לשם, הם יעזרו לך להדביק את זה בחזרה!"

            ao surprised "וואו! איך לא חשבתי על זה קודם?"

            ao "…כנראה כי לא עשיתי קוספליי בעבר, האמת."

            ao happy "לא משנה, בוא נלך לשם עכשיו!"

            stop music fadeout 1.0

            scene bg_cosplay_fix with fade

            "המקום הומה אנשים. כל כך הרבה צבעים, תחפושות, פרופס."

            "רגע, יש שם קוספליי של הילד מהבזוקה? מולך עומד קאסט."

            "יש מלא ילדים וילדות מסדרה ממש פופולרית, אבל ברח לך השם. הם מדברים על איך שסדרת ההמשך ממש גרועה ויש בה דינוזאורים. רגע, זה לא על נינג'ות?"

            "בעצם, זה לא משנה, אאוי צריכה עזרה!"

            play music bgm_convention

            show aoi happy with dissolve

            p "וואו… ממש עמוס פה, תמיד יש כל כך הרבה קוספליי בכנס?"

            ao "כמובן, מה זה כנס אנימה בלי קוספליי?"

            ao "אני רואה את עמדת התיקון. בוא נתקדם לכיוונה, אין לנו רגע לבזבז!"

            "אתם מתקדמים לכיוון פינת התיקון ורואים הרבה מאוד ציוד, הרבה ממנו כבר נמצא בשימוש על ידי קוספליירים אחרים שכנראה יש להם תקלות. מולכם נמצאת המתנדבת שאחראית על הציוד."

            am "היוש, קוראים לי עמית, במה אוכל לע–"

            am "או מיי גד… אאוי-צ'אן? ✪ ω ✪"
            with hpunch
            am "{b}קיההההההה!!! ╰(*°▽°*)╯"
            

            am "א-א-אני המעריצה הכי גדולה שלך בעולם אני לא מאמינה שאת פה, ואת עוד בקוספלייי >W<!"

            am "ר-רגע, מה קרה? הקוספליי נשבר? הוא זה ששבר אותו? (╬▔皿▔)╯"

            "עמית מצביעה כרגע עלייך."

            ao happy "האמת… שהוא בא איתי. חלק מהקוספליי שלי נפל, אני חייבת לתקן את זה לפני שיפוט הקוספליי או שלא אנצח בתחרות!"

            am "…טוב בסדר, אם הוא עוזר לך, אז הוא יכול להישאר."

            p "(וואו, מה הקטע שלה?)"

            ao smug "כשאת דמות הדגל של אמא\"י, אין ברירה אלא שיהיו לך יותר מידי מעריצים!"

            am " אייייייי! הכל כדי לעזור לך לנצח אאוי!"
            stop music fadeout 0.5
            hide aoi with dissolve
            am "בעזרת הכוח שניתן לי בתדריך מתנדבים…. אני אתקן אותך!"

            # note: sfx for magical transformation here
            play music bgm_magical volume 0.5
            pause 2.0
            "עמית שואפת נשימה עמוקה ונראה שרוח הקוספליי אוחזת בה."
            "היא אוחזת את המטה עם הכוכב השבור, ומרימה אותו אל על."
            "לרגע נראה שהדבק מרים את עצמו מהשולחן, ובחינניות רבה עושה את דרכו לכיוון הכוכב."
            "אור חזק מאיר על הכוכב... עמית מצמידה את המטה אל האור, והדבק חג סביב הכוכב והמטה באלגנטיות.
            אם מסתכלים ממש טוב, נראה כאילו הדבק יוצר מעין פפיון קסום!"
            "האור של הכוכב מאיר את עמדת תיקון הקוספליי, ואז...!"
            "באורח פלא, הכוכב מחובר בצורה מושלמת למטה, ולאחר שהוא עושה סיבוב קטן במקום, הוא מנצנץ לעיני כולם!"

            stop music fadeout 1.5
            pause 2.0
            
            am "פיו. הקוספליי יצא ממש טוב, אז יהיה חבל שיורידו לך נקודות על משהו שלא קרה באשמתך. \(@^0^@)/"

            show aoi excited with dissolve

            ao excited "וואו זה היה... מטורף!"

            p "כן, קצת לא ריאליסטי אבל אהבתי את האפקטים!"

            am "עם קצת אמונה הכל יכול לקרות, נכון אאוי-צ'אן?"

            ao smug "תתעלמי ממנו, זה הכנס הראשון שלו, הוא עוד ילמד. בכל אופן, תודה לך!"

            am "וואו…  אאוי-צ'אן הודתה לי באופן אישי… q(≧▽≦q"

            ao worry "…אולי כדאי שנלך."

            p "כן, אני גם חושב."

            "אאוי ואתה יוצאים ממתחם הקוספליי עם המטה החדש, שנראה הרבה יותר טוב מאיך שהיה כשהגעתם!"

            $ ending_progress += 1

            jump aoi_route_2
        
        # bad choice
        label aoi_route_1_bad:
            
            ao worry "ל… קפיטריה?"

            p "אני בספק שאין להם שם דבק, יש לך רעיון יותר טוב?"

            show aoi -worry

            ao "המממ…..  לא זכור לי שיש מקום ספציפי בכנס עם ציוד לקוספליי, אז זו האופציה הכי טובה!"

            p "הם שומרים על האוכל בתצוגה בדרך כזו או אחרת!"

            ao "בוא נבדוק, מקסימום נקנה בורקס בשביל להתנחם במשהו."

            stop music fadeout 1.0

            scene bg_cafeteria with fade

            show aoi sad with dissolve

            play music bgm_weird

            ao sad "…אז אין לכם שום דבק?"

            shp "יש לנו קליק."

            ao "שזה… סוג של דבק?"

            shp "זה חטיף."

            ao angry "אוף. טוב, בזבזנו על זה מלא זמן."

            ao "תודה על כלום. בוא נקנה איזה בורקס ונתחפף מפה."

            p "סליחה… באמת חשבתי שיש להם דבק!"

            ao "השופטים לא יאמינו לי!"

            ao "טוב, אגיד להם שאני עושה את הדמות מיקום מקביל או משהו."

            p "(זה לא הלך כל כך טוב… אולי אני אצליח במשימה הבאה.)"

            stop music fadeout 1.0

            jump aoi_route_2
    
    ## second event: going to the booths
    label aoi_route_2:
        
        scene bg_con_stairs with fade

        "לאחר שהיא נקראה לעמדת שיפוט הקוספליי, אאוי חוזרת לכיווני."
        
        show aoi with dissolve
        p "איך הלך השיפוט? היה בסדר?"

        if ending_progress == 1:

            ao happy "הלך מעולה! הקוספליי נראה מושלם."

            ao "והשופטים החמיאו לי על הדיוק בפרטים, במיוחד על החלק שהצלחנו להדביק בחזרה!"

            ao "אמרו שאני משדרת את נטלישיקו באופן מושלם, מבחירת הבדים עד לבובה טי החצי גמור!"

            ao "אין ספק שאני אעלה לבמה עכשיו!"
        else:
            ao worry "לא משהו. השופטים שאלו אותי לגבי החלק בקוספליי שהיה חסר."

            ao "ושאמרתי שזו גרסה מיקום מקביל…"

            ao "הם אמרו שתל אביב גדולה מספיק גם בלי יקום מקביל, וזה לא יעזור עם בעיות החניה שנטלישיקו חווה במהלך הסדרה."

            ao "אבל אני כנראה עדיין אעלה לבמה, אז זה לא נורא!"
        
        play music bgm_convention
        with hpunch
        
        ao happy "בכל מקרה! הגיע הזמן למשימה השנייה!"

        ao "כדי לתמוך בסיכויים שלי לנצח, הגיע הזמן לתמוך בעשייה מקומית!"

        menu:
            "עשייה מקומית?":
                pass
            "מה זה אומר?":
                pass
        
        ao @ excited "הדרך הכי טובה להגיע לליבם של הקהל... היא דרך מרצ'!"

        ao "ואם נראה להם שאנחנו תומכים באמנים מקומיים ונקנה מוצרים של הדמות שהם הכינו…"

        ao "לקהל לא תהיה ברירה אלא להעריך את המאמצים שלי!"

        ao "\"אהובת הקהל\" יהיה תואר יקר ערך נוסף שאזכה בו!"

        ao @ worry "…ותמיכה באמנים המוכשרים שמגיעים לכנס, כמובן."

        ao "בוא נלך ביחד לדוכני האמנים ונבחר משהו מתאים."

        menu:
            "יאללה!":
                pass
            "אני מקווה שיש להם פיגר של קוגו":
                pass
            "יאי D:":
                pass

        ao think "טוב, אז לאן הולכים? אולי רשום במפה של המתחם?"

        p "הממ… אני רואה את המפה מכאן, אבל אני לא בטוח."

        ao "נתקרב לבדוק או שחיים רק פעם אחת?"

        menu:
            "לא יכאב לנו להסתכל.":
                jump aoi_route_2_good
            "כן YOLO, חיים רק פעם אחת!":
                jump aoi_route_2_bad

        # good choice
        label aoi_route_2_good:
            
            p "לפי המפה, שדרת האמנים לא רחוקה מכאן."

            p "יש בה מלא שולחנות מסודרים, וראיתי שם גם כל מיני ציורים מוצגים."

            p "מה את חושבת, אאוי?"

            ao happy "נשמע לי כמו הזדמנות עסקית טובה!"

            ao "תוביל את הדרך!"

            scene bg_con_booth_1 with fade

            "לאחר עלייה קצרה במדרגות הגדולות, אני פה...!"

            "שדרת האמנים של הכנס!"

            "מגוון אמנים מהקהילה מציגים ומוכרים את היצירות שלהם כאן."

            "בין אם זה יצירות שקשורות לאנימות הכי חמות כרגע, או אפילו יצירות מקוריות!"

            "לאחר חיפוש קצר, אני מוצא אמנית שמוכרת מחזיק מפתחות של נטלישיקו, הדמות אליה אאוי עשתה קוספליי."

            show aoi with dissolve

            p "מה לגבי הדוכן הזה?"

            p "נראה שיש להם דברים מהסדרה שלך, ועוד הרבה סדרות אחרות!"

            p "..האמת, לדוכן שלהם יש רשתות הצגה כל כך גבוהות, שאני לא בטוח אפילו איפה הוא נגמר."

            p "הוא קצת כמו גורד שחקים."

            ao surprised "וואי, יש פה מלא דברים טובים!"

            ao "כמה מרצ'... ושל כל כך הרבה סדרות!"

            show aoi 

            ao "…אני שמחה שאני צריכה לקנות רק משהו אחד,"

            ao @ worry "כי אם לא, שירו בחיים לא יסלח על זה על זה שהייתי מחסלת לנו את התקציב."

            ao happy "אני אקח את מחזיק המפתחות הזה!"

            ao "הוא חמוד ברמות, והוא מתאים בדיוק לקוספליי שלי!"

            p "נראה שגם הטבעת עצמה של המחזיק מתאימה לשאר הקוספליי."

            p "הנה, פתחתי את הסוגר כדי שתוכלי לחבר אותו לקוספליי."

            ao "מעולה!"

            ao "יאללה, בוא נלך לכיוון האולם הראשי, עוד מעט מתחילה החזרה הגנרלית לתחרות!"
            
            stop music fadeout 1.0
            
            $ ending_progress += 1

            jump aoi_route_3

        # bad choice
        label aoi_route_2_bad:
            
            p "האמת, שמעתי על איזשהו מתחם סגור בכנס שמציגים בו דברים שקשורים לאנימה!"

            p "בואי נראה אם אני זוכר איפה זה היה…"

            p "איזשהו אולם, זה בטח מכאן…"

            stop music fadeout 1.0

            scene black with fade

            "התרוצצת עם אאוי בכל מיני מסדרונות שונים בכנס, ובסוף הצלחתם למצוא את האולם המדובר."

            scene bg_lecture with fade

            play music bgm_weird

            "רצתם ישר אל תוך חדר באמצע הרצאה, ולא לשדרת האמנים."

            p "...אופס."

            show aoi angry with dissolve

            ao "לא נראה לי שמוכרים פה מרצ'."

            ao "אין פה אף דוכן בכלל."

            ao "ואני אפילו לא יכולה לצעוק כי אני לא רוצה להפריע למרצה."

            ao embarrased " אבל אני לא יכולה להגיד שזה לא מעניין, לפחות."

            ao "אז ככה המדע הזה עובד…"

            ao angry "אבל אחרי שמסתיימת ההרצאה אנחנו חייבים לרוץ בחזרה לכיוון האולם הראשי."

            ao "אני לא רוצה לפספס את התדריך אחרי שכבר פספסתי את הדוכנים."

            p " אני מצטער, אאוי..."

            menu:
                "האם בורקס יעזור לך להרגיש יותר טוב?":
                    pass
                "רוצה מים בטעם וצבע של קולה?":
                    pass
            
            ao "אל תנסה אותי. אסור לאכול באולם בכל מקרה."

            ao embarrased "בוא פשוט נמהר."

            stop music fadeout 1.0

            jump aoi_route_3

    ## event 3: technical instructions
    label aoi_route_3:
        scene bg_stage_back with fade
        show aoi with dissolve

        ao "מעולה, הגענו לפני שהתדריך התחיל."

        ao "זה יכול היה להיות אסון אם לא הייתי מגיעה לתדריך עצמו."

        ao "אחרי כל המאמץ שהשקעתי בסקיט, זה היה אסון אם אני–"

        pause 1.0 

        ao surprised "לא הייתי… מוכנה."

        p "מה קרה אאוי?"

        play music bgm_crisis

        ao worry "הנחיות התאורה והבמה שלי… לא העברתי אותן עדיין!"

        p "מה?!"

        ao angry "נו… לא היה לי זמן! אתה יודע כמה קשה זה להתאמן על סקיט בחנייה ציבורית כל ערב?!"

        ao worry "מהר! אין לנו זמן לבזבז!"

        ao "אנחנו חייבים להעביר את ההנחיות לצוות הנכון!"

        "{color=#ff0000}{b}{size=40}הוראות הבמה נמסרות לצוות הקוספליי שבועות מראש."
        "{color=#ff0000}{b}{size=40}אאוי פשוט מנצלת את המעמד שלה כדי להעביר את הוראות הבמה לצוות בהתראה קצרה."
        "{color=#ff0000}{b}{size=40}אל תנסו את זה בבית (או בכנס)."

        p "כדאי שנרוץ, אנחנו צריכים להביא את ההוראות ל..."

        menu:
            "צוות לוגיסטיקה!":
                jump aoi_route_3_bad
            "יש צוות אולם ראשי?":
                jump aoi_route_3_good

        # good choice
        label aoi_route_3_good:
            
            stop music fadeout 1.0
            
            p "אם זה אירוע שקורה באולם הראשי… אולי יש צוות אחראי."

            ao happy "…כן! נכון! צוות אור-קולי!"

            play music bgm_weird

            ao embarrased "בעיקרון, הייתי צריכה להעביר את ההנחיות חודשים מראש."

            "הערות כותב: נכון."

            ao think "וזה היה צריך להיות חלק מההרשמה מול צוות קוספליי!"

            "הערות כותב: אם את יודעת אז למה לא עשית?"

            ao smug "ואולי זה כבר ממש מאוחר לבקש,"

            "הערות כותב: לפחות היא יודעת שזה לא בסדר?"

            ao happy "אבל זה הזמן שלי להשתמש בכוח הכי גדול שלי!"

            ao "פרוטקציה!"

            menu:
                "הגיוני בסך הכל. ":
                    pass
                "לא ידעתי שאת יכולה. ":
                    pass
                "…אהא.":
                    pass
            
            ao "יאפ!"

            ao "צוות אור-קולי אחראים לכל מה שקשור לאולם הראשי, לסאונד ולתאורה, הם בטוח יוכלו לעזור!"

            ao "בלעדיהם לא יהיה כנס, ואפילו לא תהיה תחרות קוספליי!"

            p "...אז לא כדאי שנמהר להביא להם את ההנחיות?"

            ao surprised "אה, נכון."

            ao "בוא נמצא מתנדב לשאול אותו."

            stop music fadeout 1.0

            scene bg_stage_empty with fade

            play music bgm_convention

            am "קייה! אאוי-צ'אן!!! φ(゜▽゜*)♪"

            am "…ואתה. איך אפשר לעזור?"

            show aoi embarrased with dissolve

            ao embarrased "עמית! איזה מזל שאת פה! את יכולה לדבר עם צוות אור-קולי ולהעביר להם את ההוראות האלו?"

            ao "אני יודעת שזה קצת באיחור…"

            ao "ושהן בכתב יד…"

            "הערות כותב: אל תתחילי שוב."

            ao "אבל את יכולה לשאול אותם לגבי זה?"

            p "בבקשה, עמית! אנחנו רוצים שאאוי תצליח בתחרות!"

            p "בלי תאורה, אף אחד לא יראה את הקוספליי המדהים שלה!"

            p "אם צריך להקריב מישהו כדי לרצות את הכעס שלהם, אני מוכן!"

            ao surprised "מ-מה–"

            p "(אני מקווה שלא! אני רוצה להגיע לעוד כנסים בעתיד!)"

            am "אני אעביר להם, אל חשש!"

            am "חוץ מזה, אני לא חושבת שצריך קורבן בשביל זה."

            am "זה לא כאילו שהם מנסים להחיות את אמא שלהם או משהו. זה רק הוראות במה."

            am "ובשביל אאוי… אני אעשה הכל!"

            am "בשבילך… קצת פחות."

            ao embarrased " כ-כל עוד זה יקרה."

            p "…כן."

            am "אני על זה!"

            am "אבל כדאי לך לרוץ, החזרה מתחילה!"

            ao surprised "אוי נכון!"

            ao happy "תודה רבה! נתראה בתחרות!"

            hide aoi with dissolve

            am "היא חייכה אליי… שוב…"

            p "…אני הולך לתפוס מקום באולם."

            stop music fadeout 1.0

            $ ending_progress += 1

            jump aoi_route_end

        #bad choice

        label aoi_route_3_bad:

            p "אולי… צוות לוגיסטיקה?"

            p "הם… נראה לי שהם אחראים לכל הכנס?"

            ao worry "לא… לא יודעת?"

            ao "הם אחראים על כל הציוד, אבל אני לא חושבת שהם אחראים על–"

            ao angry "טוב, אין לנו זמן!"

            ao "בוא נלך לחדר הלוגיסטיקה!"

            stop music fadeout 1.0

            scene bg_staff_hallway with fade

            "אאוי ואתה ממהרים לחדר הלוגיסטיקה, אבל שם גיליתם את האמת המרה…"

            "חדר לוגיסטיקה סגור לכל מי שהוא לא סגל."

            "אולי אם הייתם מתנדבים, אבל אין מי שיוכל לעזור לכם מהצוות שנמצא שם."

            play music bgm_weird

            show aoi embarrased with dissolve

            ao embarrased "אולי זה לא רעיון כזה טוב ללכת סתם למקומות כי נראה לנו ששם נמצא את הפתרון."

            p "לא חשבתי שיש מקומות שאסור לבאי הכנס להיות בהם."

            ao angry "הסגל של הכנס עובד מאוד קשה כדי שהכנס הזה יתקיים!"

            ao "הם צריכים לעבוד בצורה מקצועית ואחראית, ולכן יש דברים שרק הם יכולים לעשות,"

            ao "ומקומות שרק הם יכולים להיות בהם!"

            ao "חשוב לכבד אותם ואת העבודה שלהם."

            ao worry "אבל… אני צריכה למהר לאולם הראשי."

            ao "אני אתנצל בפני צוות קוספליי, ואני אבקש מהם לקחת את ההנחיות."

            p "אבל איך תשכנעי אותם להקשיב לך?"

            ao embarrased "אני... אסכים להנחות את התחרות הבאה."

            ao "אני אפילו אגרור את שירו איתי אם צריך."

            p "וואו, את חושבת שהוא יס-"

            show aoi 

            ao "אם הוא לא יסכים, אני אספר לכולם שהוא בכה בסרט הראשון של פוקימון."

            menu:
                "לגיטימי.":
                    pass
                "לגיטומי.":
                    pass
                "וואי וואי.":
                    pass

            ao worry "תאחל לי בהצלחה. אני הולכת לאולם הראשי."

            stop music fadeout 1.0

            jump aoi_route_end

    ## aoi route endings
    label aoi_route_end:
        
        scene bg_contest_outside with fade

        "עזבת את אאוי להתכונן לתחרות הקוספליי ויצאת למסדרון ליד האולם הראשי."
        
        "המסדרון עצמו הומה באנשים, בין אם זה באי כנס רגילים או קוספליירים ממולחים."
        
        "מתוך קהל האנשים, ניתן לראות רק פרופים לקוספליי מבצבצים."
        
        "בקרוב יגיע רגע האמת, וכל מה שאתה יכול לעשות זה לאחל לה בהצלחה בלב!"
        
        "לאחר קצת זמן, נפתחות הדלתות לאולם הראשי. האנשים מתחילים לזרום לתוכו, ואתה ביניהם."
        
        p "אני מקווה שאני אמצא מקום טוב…!"

        scene bg_contest_crowd with fade

        "הצלחת לעשות זאת! מצאת מקום טוב שאפשר לראות ממנו את התחרות!"

        "לידך יושבים באי כנס אחרים, ובאולם נשמעים לחשושים לגבי התחרות עצמה."

        "מי ינצח? איזה קוספליי יהיה בתחרות? האם עוד פעם יהיה קוספליי של שיאל בשמלה?"

        "ופתאום, קול מוכר מפציע."

        play sound sfx_crowd_cheer

        am "נא למלא את השורות בבקשה! אנחנו רוצים להתחיל את האירוע!"

        am "אני רוצה לראות את אאוי בתחרות אז תזדרזו!!!"

        pause 2.0

        "לאחר שתיקה מביכה שבאה אחרי הצהרת הכוונות המוזרה, נראה שכל באי האירוע נרגעו והסתדרו באולם."

        "פתאום… האורות באולם נכבים, וזרקור מתחיל להאיר על מרכז הבמה, שם נמצא…"

        "המנחה!"

        scene bg_contest_stage with fade
        play music bgm_cosplay_contest

        mtt "ערב טוב ירושלים, וברוכים הבאות לתחרות הקוספליי!"

        mtt "יש לנו מגוון קטגוריות, מגוון קוספליירים וקוספלייריות והמון פרסים שווים!"

        mtt "האם אתם מוכנים?"

        play sound sfx_crowd_cheer

        "כן כן כן!"

        # check ending status

        if ending_progress >= 2:
            jump aoi_good_end
        else:
            jump aoi_bad_end

        # aoi good ending
        label aoi_good_end:

            p "רגע האמת מגיע… העליה של אאוי לבמה!"

            stop music fadeout 1.0

            pause 1.0
            
            mtt "והמתמודדת הבאה בקטגוריית יחידים מתחילים היא… אתם מכירים אותה היטב, אאוי!"

            mtt "שעושה קוספליי לדמות \"הנסיכה נטלישיקו\" מחלק 8 של הסדרה \"הפעם ההיא שהפכתי לסליים, אבל בצפון תל אביב\"!"

            play sound sfx_crowd_cheer

            "לאחר מחיאות כפיים סוערות מהקהל, המנחה יורד מהבמה והאור נכבה שוב, כדי לכבד את העלייה הראשונה של אאוי לבמת הקוספליי!"

            scene bg_contest_stage_front with fade

            pause 1.0

            # note: add light effects here
            show aoi with dissolve
            play music bgm_magical volume 0.5

            "המוזיקה מתחילה בדיוק ברגע הנכון, אאוי קופצת לבמה והזרקור מאיר אותה בצורה מושלמת!"
            
            # note: have aoi moving around here
            show aoi at left_to_right
            
            "בזמן שהתאורה והמוזיקה מתאימות את עצמן לתנועות שלה, הקוספליי שלה זוהר."

            "כל החלקים שלו מוצגיםבצורה מחמיאה, מדויקת וללא כל פגם. כתוספת להכל, מחזיק המפתחות שמתנדנד לו כחלק מהקוספליי תופס את תשומת הלב של השופטים ושל הקהל שיושב מקרוב!"

            "לאחר שאאוי עושה כמה פוזות אייקוניות של הנסיכה נטלישיקו בשביל הצלמים והקהל, היא קדה קידה מסורתית לצליל מחיאות כפיים סוערות מהקהל."

            play sound sfx_crowd_cheer

            ao happy "תודה רבה!"

            hide aoi with dissolve

            "ובמהירות, היא יורדת מהבמה כדי שהמנחה יוכל לעלות בחזרה."

            stop music fadeout 1.0

            scene bg_contest_stage with fade

            "לאחר שנרגעות מחיאות הכפיים, המנחה חותם את העלייה לבמה של אאוי."

            play music bgm_cosplay_contest

            mtt "…וואו, יש סיבה שהיא הפרזנטורית שלנו, איזו הופעה!"

            "המנחה מכניס אוויר לריאות, כדי להתכונן שוב להגיד את השם המסובך והמתוסבך של הדמות והסדרה…"

            mtt "תודה רבה לאאוי שעשתה קוספליי ל\"נסיכה נטלישיקו\" מחלק 8 של הסדרה \"הפעם ההיא שהפכתי לסליים, אבל בצפון תל אביב\"!"

            "והוא מתנשף לרגע בכל זאת. לאחר שהוא נרגע קצת, הוא ממשיך."

            "ועכשיו למתמודד הבא…"

            stop music fadeout 1.0
            scene black with fade

            pause 1.0

            scene bg_contest_crowd with fade

            "כעת, אחרי שכל המתמודדים מכל הקטגוריות הוצגו, הגיע הזמן של השופטים להכריז על המנצחים."

            p "אני לא מקנא בשופטים שצריכים לדרג את הקוספליירים… אבל אני מאוד מקווה שאאוי תזכה!"

            scene bg_contest_stage with fade

            play music bgm_cosplay_contest

            mtt "ובכן, גבירותיי ורבותיי, וכל מי שלא עונה לקטגוריות האלו… יש לנו זוכים!"

            "השופטים המוכשרים שלנו כבר ממוקמים על הבמה עם הפרסים, ועכשיו אני אתן להם להציג את הזוכים."

            jdg "היה לנו מאוד קשה השנה לבחור מנצחים. אם הייתי יכולה, הייתי נותנת פרס לכל מי שהשתתף!"

            jdg "אבל מכיוון שאנחנו רוצים שיהיו עוד כנסים ותחרויות ולא לשרוף את התקציב… בחרנו שלושה."

            # note: weird laugh sfx here?

            "צחוק קטן עולה מהקהל, שלא בטוח אם היא הייתה רצינית או לא. מי אמר שכל הכיף חייב לבוא רק מהמנחה?"

            "ובכן, בקטגוריית יחידים מתחילים, במקום השלישי…"

            play sound sfx_drumroll

            pause 3.0

            jdg " זכה אילן בקוספליי ל\"רנג'י\" מ\"איש מכסחת הדשא הביתית\"!"

            p "וואו, הוא באמת היה טוב! לא האמנתי שהוא הצליח לבנות מכסחת דשא ביתית רק מקרטונים!"

            "ונראה שגם הקהל חושב ככה, כי מלוות אותו מחיאות כפיים סוערות כאשר הוא לוקח את התעודה והרוזטה מהשופטים. "

            jdg "במקום השני… "

            play sound sfx_drumroll

            pause 3.0

            jdg "זכתה תמר בקוספליי שלה ל\"חנוכה\" מ\"איזה שיר מזמור! איידוליות בעירוני ב'!\""

            p "הקוספליי ממשחק המובייל הכי חם כרגע! זה היה מדהים איך שהיא התאימה את התנועות שלה בדיוק לתנועות במשחק, ורמת הדיוק בפרטים של השמלה שהדמות לובשת בזמן ההופעה שהייתה להם במתנ\"ס העירוני!"

            p "אאוי, הגיע הזמן שלך…!"

            jdg "ובמקום הראשון, בהופעה הראשונה שלה בתחרות בצורה שהרשימה את כולנו…"

            play sound sfx_drumroll

            pause 3.0

            jdg "אאוי בתור \"הנסיכה נטלישיקו\" מחלק 8 של הסדרה \"הפעם ההיא שהפכתי לסליים, אבל בצפון תל אביב\"!"

            play sound sfx_crowd_cheer

            scene cg_aoi_good

            "עשיתם את זה."

            "אאוי זכתה!"

            "הקהל משתגע ומוחא כפיים! מעריצים שרופים של אאוי מתחילים לשיר!"

            "אאוי זכתה בקטגוריית יחידים מתחילים!"

            "היא עושה את דרכה במהירות לבמה, משתדלת לא ליפול ברגע הקריטי הזה."

            "אאוי כל כך שמחה, שהבעת הפנים  שלה נתקעה בין שמחה לבין שוק טוטאלי."

            "היא מקבלת את התעודה והרוזטה מהשופטים, וממהרת לצד הבמה כדי לעמוד עם שאר הזוכים בקטגוריית \"יחידים מתחילים\"."

            "היא לוחצת להם את היד (או את מכסחת הדשא הביתית), מחבקת אותם ומצטלמת איתם."

            "תחרות הקוספליי הראשונה של אאוי נרשמת כהצלחה מסחררת."

            scene black with fade

            "{b}וכל הכבוד לך, עזרת לאאוי לזכות בתחרות הקוספליי!"

            "{b}אאוי תזכור את זה, ואנחנו מקווים שגם אתם!"

            "{b}סוף טוב הכל טוב :)"

            $ persistent.aoi_ending = True
            
            return

        # aoi bad ending
        label aoi_bad_end:

            p "רגע האמת מגיע… העליה של אאוי לבמה!"

            stop music fadeout 1.0

            pause 1.0

            mtt "והמתמודדת הבאה בקטגוריית יחידים מתחילים היא… אתם מכירים אותה היטב, אאוי!"

            mtt "שעושה קוספליי לדמות… אממ…"

            mtt "הנסיכה נטלישוקו? הנסיכה נטלישנקו? ...נטלישיקו, מחלק 8 של הסדרה \"הפעם ההיא שהפכתי לסליים, אבל בצפון תל אביב\"!"

            pause 1.0

            mtt "כפיים!"

            play sound sfx_awkward_clap

            p "אוי לא."

            scene bg_contest_stage_front with fade

            show aoi with dissolve

            pause 1.0

            play music bgm_magical
            
            "אוי. לא. המוזיקה לא הופעלה בתזמון הנכון."

            stop music

            hide aoi 
            "אאוי חוזרת אחורה, ומחכה לסימן מצוות אור-קולי כדי לעלות שוב."

            pause 2.0

            "אולי עכשיו?"

            "עכשיו."

            play music bgm_magical

            pause 1.0

            show aoi worry with dissolve

            "אאוי עולה לבמה. אתה רואה שהיא משתדלת לשמור על קור רוח למרות התקלה הקטנה."

            "ברגע שאאוי מתחילה לצבור מומנטום לקראת הסיום הגדול–"

            stop music

            play sound sfx_gasp

            "הכוכב במטה של אאוי נתקע בחצאית שלה ונתלש מהמקום!"

            hide aoi with dissolve 

            "נראה שאאוי לא מרוצה בכלל מעלייתה לבמה. היא נעצרת במקום, מסרבת להמשיך ויורדת משם."

            scene black with fade

            "…וזו הייתה אאוי! מחיאות כפיים בבקשה!"

            play sound sfx_awkward_clap

            "הקהל מוחא כפיים בצורה לא מאוד נלהבת, שנגמרת אחרי כמה שניות."

            "…ובכן. המתמודד הבא הוא…"

            pause 1.0

            scene bg_contest_stage with fade

            "כעת, אחרי שכל המתמודדים מכל הקטגוריות הוצגו, הגיע הזמן של השופטים להחליט על המנצחים."

            p "אני מקווה מאוד שאאוי תזכה…"

            p "למרות... שעם כל מה שהשתבש בהופעה שלה, אני בעיקר מקווה שהיא תזכה במקום כלשהו."

            "לאחר מעט התדיינות, השופטים עולים על הבמה כדי לחלק את הפרסים."

            mtt "ובכן, גבירותיי ורבותיי, וכל מי שלא עונה לקטגוריות האלו… יש לנו את הזוכים!"

            mtt "השופטים המוכשרים שלנו כבר ממוקמים על הבמה עם הפרסים, ועכשיו אני אתן להם להציג את הזוכים."

            "המנחה מעביר את המיקרופון לאחת השופטות, שכבר מוכנה עם רשימת השמות."

            jdg "היה לנו מאוד קשה השנה לבחור מנצחים. אם הייתי יכולה, הייתי נותנת פרס לכל מי שהשתתף!"

            jdg "אבל מכיוון שאנחנו רוצים שיהיו עוד כנסים ותחרויות ולא לשרוף את התקציב… בחרנו שלושה."

            "צחוק קטן עולה מהקהל, שלא בטוח אם היא הייתה רצינית או לא. מי אמר שכל הכיף חייב לבוא רק מהמנחה?"

            jdg "ובכן, בקטגוריית יחידים מתחילים, במקום השלישי…"

            "השופטים מכריזים אחד אחד על הזוכים, המקום השלישי… לא אאוי. גם לא המקום השני. "

            "אתה מחזיק אצבעות כל כך חזק שהן כבר אדומות, בטח עם כל מה שעברתם בכנס אאוי תהיה-"

            jdg "ובמקום הראשון…"

            play sound sfx_drumroll

            pause 3.0

            jdg " זכה אילן בקוספליי ל\"רנג'י\" מ\"איש מכסחת הדשא הביתית\"!"
            
            "…זה נגמר."

            show aoi angry with dissolve

            "אאוי לא מחכה אפילו שנייה אחת ועולה לבמה, עוד לפני שהמנצח מספיק  לעלות לקחת את הפרס שלו, וחוטפת לשופטת את המיקרופון מהיד בכוח."

            play music bgm_bad_ending

            scene cg_aoi_bad with hpunch

            ao "מה אתם רציניים עכשיו?? מה נסגר?!"

            ao "שנים שאני מייצגת את הכנסים בכבוד ואפילו לתת לי פרס ניחומים לא בכבוד שלכם?!"

            ao "אתם יודעים כמה קשה זה לעמוד בלי לזוז באותה הפוזה כל פעם בשביל המאיירים כדי שהם יציירו אותי לפרסומים?!"

            ao "חשבתם אי פעם כמה מביך זה שיש סרטונים שלי ברשת עם פאה זולה?!"

            ao "שלא נדבר אפילו על זה שכל הזמן משווים אותי לדמות הזו! אתם יודעים מי! אני דמות משל עצמי!"

            ao "כולה רציתי לעשות קוספליי לנסיכה נטלישיקו!"

            ao " נ-טל-י-שי-קו! הנה! כמה קשה לאיית את זה?!"

            ao "אבל יודעים מה? לא צריך"

            ao "אם אף אחד לא הולך להעריך אותי, אני לא צריכה אתכם!"

            ao "אני הולכת הביתה!"

            ao "או אולי לכנס בחו\"ל!"

            ao "נמאס לי!!!"

            scene black with fade

            play sound sfx_mic_drop

            "אאוי מטיחה את המיקרופון לרצפה בחוזקה, ההדהוד של הנפילה מטלטל את האולם. אאוי יורדת מהבמה בזעם ועוזבת את האולם בטריקת דלתיים מרעישה. "

            "{b}לתחרות הזו היא לא תחזור. הכבוד שלה נפגע וגם הרגשות שלה."

            "{b}אאוי תזכור את זה לפעמים הבאות."

            "{b}בעיקרון הגעת לסוף הרע :("

            $ persistent.aoi_ending_bad = True
            return

    return
